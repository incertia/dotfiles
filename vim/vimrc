"" editing
set ts=2 sw=2 sts=2 et ai number
" set cinoptions=g-1,:0,(0
set cinoptions=g0,:0,(0
set nohlsearch
set guicursor=

" appearance
set t_Co=256 title scrolloff=2 wrap showmatch tw=80
set list listchars=tab:\|\ ,trail:.
set laststatus=2
syntax on

"" other stuff
set matchtime=1
let mapleader="\\"
let localleader=","
set sessionoptions-=options

" latex should be default
let g:tex_flavor = "latex"

" vim-plug
call plug#begin('~/.vim/plugged')

Plug 'Shougo/vimproc.vim',          { 'do': 'make' }

" Required for operations modifying multiple buffers like rename.
set hidden
set completeopt+=menuone,noselect
if has('nvim')
  Plug 'Shougo/deoplete.nvim', { 'do' : ':UpdateRemotePlugins' }
  let g:deoplete#enable_at_startup = 1
  " Plug 'neovim/nvim-lspconfig'
  " set omnifunc=v:lua.vim.lsp.omnifunc
else
  " mucomplete
  Plug 'lifepillar/vim-mucomplete'
endif

Plug 'autozimu/LanguageClient-neovim', {
  \ 'branch': 'next',
  \ 'do': 'bash install.sh',
  \ }
let g:LanguageClient_serverCommands = {
  \ 'c'       : ['clangd'],
  \ 'cpp'     : ['clangd'],
  \ 'haskell' : ['hie'],
  \ 'rust'    : {
    \ 'name'  : 'rust-analyzer',
    \ 'command': ['rustup', 'run', 'nightly', 'rust-analyzer'],
    \ 'initializationOptions': {
      \ 'cargo': {
        \ 'loadOutDirsFromCheck': v:true
      \ },
      \ 'procMacro': {
        \ 'enable': v:true
      \ },
      \ 'diagnostics': {
        \ 'disabled': [ 'use-field-shorthand' ]
      \ },
    \ }
  \ }
\ }
let g:LanguageClient_useVirtualText = "No"
let g:LanguageClient_hoverMarginSize = 0

" (Optional) Multi-entry selection UI.
Plug 'junegunn/fzf', { 'dir' : '~/.fzf', 'do' : './install --bin' }

" echos docstrings
Plug 'Shougo/echodoc'
if has('nvim')
  set noshowmode
  let g:echodoc#type = 'floating'
else
  let g:echodoc#type = "echo"
  set cmdheight=2
endif
let g:echodoc#enable_at_startup = 1

" language plugins
Plug 'itchyny/vim-haskell-indent',  { 'for': 'haskell'  } " make sure to load vim-haskell-indent before haskell-vim
Plug 'neovimhaskell/haskell-vim',   { 'for': 'haskell'  }
Plug 'petRUShka/vim-sage',          { 'for': 'sage'     }
Plug 'idris-hackers/idris-vim',     { 'for': 'idris'    }
Plug 'rust-lang/rust.vim',          { 'for': 'rust'     }
let g:rust_recommended_style = 0
Plug 'cespare/vim-toml',            { 'for': 'toml'     }
Plug 'stephpy/vim-yaml',            { 'for': 'yaml'     }
Plug 'plasticboy/vim-markdown',     { 'for': 'markdown' }

Plug 'flazz/vim-colorschemes'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-surround'
let g:surround_no_mappings = 1
let b:surround_indent = 0

" :Bd to close buffers without closing the window
Plug 'moll/vim-bbye'

Plug 'sotte/presenting.vim'

nnoremap <silent> <C-p>             :<C-u>CtrlP<CR>
nnoremap <silent> <leader><C-p>     :<C-u>CtrlP<SPACE>$HOME<CR>
Plug 'ctrlpvim/ctrlp.vim',          { 'on': 'CtrlP' }
Plug 'scrooloose/nerdtree',         { 'on': 'NERDTreeToggle' }
Plug 'godlygeek/tabular',           { 'on': 'Tabularize' }


call plug#end()

function! s:NewScratch(name)
  if (a:name == "")
    tabnew
  else
    execute "tabnew " . a:name
  endif
  setl buftype=nofile
  setl bufhidden=hide
  setl noswapfile
endfunction

" windowswap
let g:windowswap_map_keys = 0

" tabularize
let g:haskell_tabular = 1

" tab navigation
function! TabSwitch(tabnum)
  call feedkeys(a:tabnum . "gt", "n")
endfunction
command! -nargs=1 TabSwitch :call TabSwitch(<f-args>)
nnoremap <C-x><C-t>          :TabSwitch<SPACE>
nnoremap <C-x>t              :TabSwitch<SPACE>

filetype plugin indent on

set viewoptions-=options

"" autocmd stuff
if has("autocmd")
  " Uncomment the following to have Vim jump to the last position when
  " reopening a file
  " au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif

  " Set scripts to be executable from the shell
  au BufWritePost * if getline(1) =~ "^#!" | if getline(1) =~ "/bin/" | silent execute "!chmod +x %" | endif | endif
  au BufWritePost * if getline(1) =~ "^#!" | if getline(1) =~ "zsh" | silent execute "setfiletype zsh" | endif | endif

  "" save/restore views when exiting/entering vim
  au BufWrite * mkview
  au BufRead * silent! loadview

  " tags the 81st column to ColorColumn
  au BufEnter,WinEnter * call matchadd('ColorColumn', '\%81v', 100)

  " Uncomment the following line to disable auto commenting
  " au FileType * setlocal formatoptions-=c formatoptions-=r formatoptions-=o
  " au FileType * setlocal formatoptions+=c formatoptions+=r formatoptions+=o
  au FileType tex setlocal textwidth=80

  autocmd FileType markdown
    \ setlocal formatoptions-=q |
    \ setlocal formatlistpat=^\\s*\\d\\+\\.\\s\\+\\\|^\\s*\[-*+]\\s\\+
endif

"folding settings
set foldmethod=indent   " fold based on indent
set foldnestmax=16      " deepest fold is 16 levels
set nofoldenable        " don't fold by default
set foldlevel=100       " yay

" number formats
set nf=alpha,octal,hex
nnoremap <C-x> <nop>
nnoremap <C-d> <C-x>

" split to the right and below
set splitbelow splitright

" map <F1> to deleting trailing whitespace and clearing empty lines
function! KillWhiteThings()
  let save_cursor = getpos(".")
  :silent! %s#\($\n\s*\)\+\%$##
  :silent! %s/\s\+$//g
  call setpos('.', save_cursor)
endfunction

nnoremap <silent> <F1>  :call KillWhiteThings()<CR>
nnoremap <silent> <F2>  :set hlsearch! hlsearch?<CR>
nnoremap <silent> <F3>  :tabonly<CR>
nnoremap <silent> <F4>  :q!<CR>

" reflow bindings
nnoremap Q              gqgq
vnoremap Q              gq

" emacs <C-x><C-c> with prompt is really nice
nnoremap <C-x><C-x>     :confirm qa<CR>

" switch files with arrow keys
nnoremap <DOWN>         :bnext<CR>
nnoremap <UP>           :bprev<CR>

" tabs
nnoremap <C-t>          :tabnew<SPACE>
let g:newtab_command = ":tabnew\<CR>"
nnoremap <C-y>          @=g:newtab_command<CR>
nnoremap <S-TAB>        gt

" map space to executing the q macro for easy qq recording
nnoremap <SPACE>        @q

" who cares about ex mode. map QQ to reflow paragraphs
" nnoremap QQ             gqap
" vnoremap QQ             gq

" save shortcut
inoremap <C-s>          <ESC>:w!<CR>a
nnoremap <C-s>          :w!<CR>

" split workflow
nnoremap <C-h>          <C-w>h
nnoremap <C-j>          <C-w>j
nnoremap <C-k>          <C-w>k
nnoremap <C-l>          <C-w>l
nnoremap <leader>-      <C-w>-
nnoremap <leader>=      <C-w>+
" because we remmaped <C-l>
nnoremap <leader><C-l>  <C-l>

" scroll up to <C-q> (don't need this to be visual block)
nnoremap <C-q>          <C-y>

" this helps with long lines
nnoremap j              gj
nnoremap k              gk
nnoremap gj             j
nnoremap gk             k
vnoremap j              gj
vnoremap k              gk
vnoremap gj             j
vnoremap gk             k

function! Scroll(dir)
  let lines = winheight(0)

  if(a:dir == "up")
    let cmd = "\<C-y>"
  elseif(a:dir == "down")
    let cmd = "\<C-e>"
  else
    let cmd = "\<Esc>"
  endif

  call feedkeys(lines . cmd, "n")
endfunction

"" All of the following settings were taken from Damian Conway
"====[ Make the 81st column stand out ]====================

" EITHER the entire 81st column, full-screen
" highlight ColorColumn ctermbg=magenta
" set colorcolumn=81

" OR ELSE just the 81st column of wide lines
hi ColorColumn ctermbg=white

nnoremap  ;  :
nnoremap  :  ;
vnoremap  ;  :
vnoremap  :  ;
nnoremap    v   <C-V>
nnoremap <C-V>     v
vnoremap    v   <C-V>
vnoremap <C-V>     v

if has('nvim')
  " use <ESC> to get out of insert mode in neovim terminals
  tnoremap <ESC> <C-\><C-n>
endif

if has("autocmd")
  au CompleteDone * pclose
endif

function SetLSPShortcuts()
  if has_key(g:LanguageClient_serverCommands, &filetype)
    nmap <buffer> <silent> <leader><F5> <Plug>(lcn-menu)
    nmap <buffer> <silent> <leader>ca   <Plug>(lcn-codeAction)
    nmap <buffer> <silent> <leader>gd   <Plug>(lcn-definition)
    nmap <buffer> <silent> <leader>gi   <Plug>(lcn-implementation)
    nmap <buffer> <silent> <leader>xx   <Plug>(lcn-references)
    nmap <buffer> <silent> <leader>K    <Plug>(lcn-hover)
    nmap <buffer> <silent> K            <Plug>(lcn-hover)
    nmap <buffer> <silent> <leader>dd   <Plug>(lcn-explain-error)
    nmap <buffer> <silent> <leader>d[   <Plug>(lcn-diagnostics-next)
    nmap <buffer> <silent> <leader>d]   <Plug>(lcn-diagnostics-prev)
    nmap <buffer> <silent> <leader>ff   <Plug>(lcn-format)
    " au CursorHold * sil call LanguageClient#explainErrorAtPoint({'silent': v:true})
  endif
endfunction()

"augroup LSP
"  autocmd!
"  autocmd FileType c,cpp,haskell call SetLSPShortcuts()
"augroup END
"

" function SetLSPShortcuts()
"   nnoremap <silent> <leader>gD    :lua vim.lsp.buf.declaration()<CR>
"   nnoremap <silent> <leader>gd    :lua vim.lsp.buf.definition()<CR>
"   nnoremap <silent> <leader>gd    :lua vim.lsp.buf.implementation()<CR>
"   nnoremap <silent> <leader>xx    :lua vim.lsp.buf.references()<CR>
"   nnoremap <silent> <leader><C-k> :lua vim.lsp.buf.signature_help()<CR>
"   nnoremap <silent> <leader>K     :lua vim.lsp.buf.hover()<CR>
"   nnoremap <silent> K             :lua vim.lsp.buf.hover()<CR>
"   nnoremap <silent> <leader>dd    :lua vim.lsp.diagnostic.show_line_diagnostics()<CR>
"   nnoremap <silent> <leader>[d    :lua vim.lsp.diagnostic.goto_prev()<CR>
"   nnoremap <silent> <leader>]d    :lua vim.lsp.diagnostic.goto_next()<CR>
"   nnoremap <silent> <leader>f     :lua vim.lsp.buf.formatting()<CR>
"   " autocmd! CursorHold *           lua vim.lsp.diagnostic.show_line_diagnostics()
"   " set updatetime=200
"   sign define LspDiagnosticsSignError text=✖ texthl=LspDiagnosticsSignError linehl= numhl=
"   sign define LspDiagnosticsSignWarning text=⚠ texthl=LspDiagnosticsSignWarning linehl= numhl=
"   sign define LspDiagnosticsSignInformation text=ℹ texthl=LspDiagnosticsSignInformation linehl= numhl=
"   sign define LspDiagnosticsSignHint text=➤ texthl=LspDiagnosticsSignHint linehl= numhl=
" endfunction()

" augroup LSP
"   autocmd! FileType c,cpp,haskell,rust call SetLSPShortcuts()
"   autocmd! FileType c,cpp,haskell,rust echo "yolo"
" augroup END

autocmd FileType * call SetLSPShortcuts()

" set the colorscheme
if has("gui_running")
  set background=light
  set guioptions-=T
  set guioptions-=m
  set guifont=DejaVu\ Sans\ Mono\ for\ Powerline\ 9
endif
colorscheme Tomorrow-Night

hi Normal      ctermbg=NONE
hi NonText     ctermbg=NONE
hi LineNr      ctermfg=blue
hi Folded      ctermbg=NONE
hi SpecialKey  ctermbg=NONE
hi VertSplit   ctermbg=NONE
hi SignColumn  ctermbg=NONE
hi SpellBad    cterm=underline ctermbg=NONE ctermfg=red
hi SpellCap    cterm=underline ctermbg=NONE ctermfg=magenta
hi String      ctermbg=NONE ctermfg=cyan
hi ColorColumn ctermbg=white ctermfg=black
hi Special     ctermfg=magenta
hi Label       ctermfg=green
hi Statement   ctermfg=red
hi PreProc     ctermfg=lightcyan
hi cInclude    ctermfg=lightcyan
hi Todo        ctermbg=darkred ctermfg=white

" enable powerline if it exists and we are not in a tty
if (!has('nvim') && !(&term=~'linux') && isdirectory(expand("~/.vim/powerline"))) || has("gui_running")
  set rtp+=~/.vim/powerline/powerline/bindings/vim/
endif

" leader command section
nnoremap <silent> <leader>so      :so<SPACE>%<CR>
nnoremap <silent> <leader>vc      :e<SPACE>~/.vim/vimrc<CR>
nnoremap <silent> <leader>la      :e<SPACE>~/texmf/tex/latex/incertia/incertia.sty<CR>
nnoremap <silent> <leader>;       m`A;<ESC>``
nnoremap <silent> <leader>gs      :NewScratch<CR>
nnoremap          <leader>gS      :OpenScratch<SPACE>

nnoremap <silent> <leader>n  :NERDTreeToggle<CR>

" tab bindings
nnoremap <leader>1           1gt
nnoremap <leader>2           2gt
nnoremap <leader>3           3gt
nnoremap <leader>4           4gt
nnoremap <leader>5           5gt
nnoremap <leader>6           6gt
nnoremap <leader>7           7gt
nnoremap <leader>8           8gt
nnoremap <leader>9           9gt
nnoremap <silent> <leader>0  :tablast<CR>

command!          NewScratch      :call s:NewScratch("")
command! -nargs=1 OpenScratch     :call s:NewScratch(<f-args>)

let g:tabmoveleft_cmd  = ":-tabmove\<CR>"
let g:tabmoveright_cmd = ":+tabmove\<CR>"
nnoremap <silent> <leader>hh @=g:tabmoveleft_cmd<CR>
nnoremap <silent> <leader>ll @=g:tabmoveright_cmd<CR>
nnoremap <leader>j           gt
nnoremap <leader>k           gT

" surround.vim
nmap <leader>ds  <Plug>Dsurround
nmap <leader>cs  <Plug>Csurround
nmap <leader>cS  <Plug>CSurround
nmap <leader>ys  <Plug>Ysurround
nmap <leader>yS  <Plug>YSurround
nmap <leader>yss <Plug>Yssurround
nmap <leader>ySs <Plug>YSsurround
nmap <leader>ySS <Plug>YSsurround
xmap <leader>S   <Plug>VSurround
xmap <leader>gS  <Plug>VgSurround

nnoremap <silent> <leader>ww :call WindowSwap#EasyWindowSwap()<CR>

vnoremap a= :Tabularize /=<CR>
vnoremap a; :Tabularize /::<CR>
vnoremap a- :Tabularize /-><CR>
